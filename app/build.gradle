import com.android.ddmlib.DdmPreferences

import java.security.InvalidParameterException

apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "tech.pauly.findapet"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "tech.pauly.findapet.EspressoTestRunner"
        DdmPreferences.setTimeOut(60000)
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all { variant ->
            String environment = project.property("findapet.environment")
            if (environment != "prod" && environment != "espresso") {
                throw new InvalidParameterException("Invalid environment property '${environment}'\n" +
                        "Valid environments are prod and espresso.")
            }

            buildConfigField "String", "ENVIRONMENT", "\"${environment}\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    aaptOptions {
        cruncherEnabled false
    }

    sourceSets {
        androidTest {
            setRoot "src/androidTest"
            res.srcDir "src/androidTest/res"
        }
    }
}

project.afterEvaluate {
    def allTasks = getGradle().getStartParameter().getTaskRequests().toString()
    if (allTasks.contains("AndroidTest")
            && !allTasks.contains("generate")
            && project.property("findapet.environment") != "espresso") {
        throw new InvalidParameterException("You're trying to run espresso tests but haven't set your environment to espresso.")
    }
    if (!allTasks.contains("AndroidTest")
            && allTasks.toString() != "[]"
            && project.property("findapet.environment") == "espresso") {
        throw new InvalidParameterException("You're trying to run the app but you've set your environment to espresso.")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Support Library
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'

    implementation 'com.squareup.picasso:picasso:2.5.0'
    compile 'jp.wasabeef:picasso-transformations:2.2.1'

    //Architecture Components
    implementation "android.arch.lifecycle:extensions:1.1.1"

    //Dagger 2
    implementation 'com.google.dagger:dagger:2.14.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.14.1'
    implementation 'com.google.dagger:dagger-android-support:2.14.1'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.14.1'

    //Rx
    implementation "io.reactivex.rxjava2:rxjava:2.1.13"
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-simplexml:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    //Unit Tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.11.0'
    testImplementation('com.squareup.assertj:assertj-android:1.1.1') {
        exclude module: 'support-annotations'
    }

    //Espresso Tests
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation('com.android.support.test:runner:1.0.0') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:3.0.0') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation('com.android.support.test.espresso:espresso-intents:3.0.0') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    }
    androidTestAnnotationProcessor 'com.google.dagger:dagger-compiler:2.14.1'
    androidTestAnnotationProcessor 'com.google.dagger:dagger-android-processor:2.14.1'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.9.0'
    androidTestImplementation 'commons-io:commons-io:2.4'
}
